if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
android {
    resourcePrefix "me_" //给 Module 内的资源名增加前缀, 避免资源名冲突
    compileSdkVersion buildv.compile_sdk
    File propFile = new File(rootDir.getAbsoluteFile(), 'config.properties')
    if (propFile.exists()) {
        Properties props = new Properties()
        props.load(new InputStreamReader(new FileInputStream(propFile), "utf-8"))
        defaultConfig {
            if (isRunAlone.toBoolean()) {
                // 单独调试时添加 applicationId ，集成调试时移除
                applicationId "com.app.login"
            }

            minSdkVersion buildv.min_sdk
            targetSdkVersion buildv.target_sdk
            versionCode 1
            versionName "1.0"
            kapt {
                arguments {
                    arg("AROUTER_MODULE_NAME", project.getName())
                }
            }
        }
        sourceSets {
            main {
                // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
                if (isRunAlone.toBoolean()) {
                    manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                }
            }
        }
        signingConfigs {
            debug {
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWORD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            }
            release {
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWORD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            }
        }

        buildTypes {
            debug {
                buildConfigField "boolean", "LOG_DEBUG", "true"
                buildConfigField "String", "HOST", props["HOST_DEBUG"]
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.debug
            }
            release {
                buildConfigField "boolean", "LOG_DEBUG", "false"
                buildConfigField "String", "HOST", props["HOST_RELEASE"]
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        // for view binding :
        viewBinding = true
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':lib:lib_common')
    kapt "com.alibaba:arouter-compiler:$depv.arouter_compiler"
}

